#To edit and compare internal_properties, use WINDEV integrated tools.
#Internal properties refer to the properties of controls in windows, reports, etc.
info :
 name : Procedures_globales
 major_version : 28
 minor_version : 0
 type : 7
 description : ""
 subtype : 0
procedure_set :
 identifier : 0x15645b67005f346d
 internal_properties : CAAAAAgAAABGLu41kG7fjQV3iS4F72qmnKaNh5694reolNKIW0iw
 code_elements :
  internal_properties : CAAAAAgAAAAdC5DaGsntqmfkq1J4p6eipmeEO//QiIdypV0FB6KY+wiPKnW2KoJpaKmeXvb1NGpH4KGGthHXu/V4LqeilI4jnJwO5Q==
  type_code : 31
  p_codes : []
  procedures :
   -
     name : bCPInvalide
     internal_properties : CAAAAAgAAAAiI5sFw2zUAabq90B3QeNm+CF2Rdr34aRle4rUMJHAhy7L+Xmfo/EUEQ5TQRBYbmHc29Uw2pR5iCff5ObHzvn8mV9z3NBuZ2oH2KYJM67X3Esgmz6lwJ90Vjh9dquGZ6azaHJZWndep5jUBp35
     procedure_id : 1541457470413616237
     type_code : 15
     code : |1+
      fonction bCPInvalide(sCP,sPays)
      
      // Renvoie Vrai si le code postal est incorrect
      si taille(SansEspace(sCP))<>5 alors
      	si sPays~="France" alors
      		erreur("Veuillez indiquer les 5 chiffres de votre code postal.")
      		renvoyer vrai
      	sinon
      		// Etranger
      		si pas OuiNon(	"Le code postal indiqué semble incomplet."+rc+rc+...
      						"Voulez-vous enregistrer quand même le code postal saisi ?") alors   
      			renvoyer vrai
      		fin
      	fin
      FIN
      
      renvoyer faux
      
     type : 458752
   -
     name : bCPVilleIncorrect
     procedure_id : 1541457470413681773
     type_code : 15
     code : |1+
      fonction bCPVilleIncorrect(sCP,sVille)
      
      // Renvoie Vrai si la correspondance CP/Ville n'est pas trouvée
      HLitRecherchePremier(CEDEX,CODEPOSTAL,sCP)
      tantque HTrouve(CEDEX)
      	si sVille~=CEDEX.VILLE alors renvoyer faux
      	HLitSuivant(CEDEX,CODEPOSTAL)
      fin
      
      si ouinon(	"Le code postal [%SansEspace(sCP)%] semble incorrect par rapport à la ville : [%SansEspace(sVille)%]",...
      			"Enregistrer quand même ?") alors
      	renvoyer faux		
      FIN
      
      renvoyer vrai
      
     type : 458752
   -
     name : bIncorrect
     internal_properties : CAAAAAgAAACC1YZC4GQifJnASaW/HeVzZXsy1az4kjgLpo4wgNzONt+myrWdKQ8sV3WF56eyMI5MCCsHln36XZA5HrosNWf5XjXVCVMOdRZcDi4HaxFXMY5aAykknt6EQp6kmxh8MGGCXSKpoXlF2eOSvc8Ma9GcnvH7GZK5HydovIaLdCO3GZSS
     procedure_id : 1541457470413747309
     type_code : 15
     code : |1+
      fonction bIncorrect(s,bChiffresInterdits=faux)
      
      // Vérifie la validité d'un champ saisi
      
      // En entrée : 
      // s = la chaine saisie à tester
      // bChiffresInterdits = Vrai si les chiffres ne sont pas autorisés (nom par ex.)
      
      // En sortie : booléen signalant si la chaîne saisie est incorrecte
      
      s=SansEspace(s)
      i est un entier
      
      // Chaine vide ?
      si s="" alors renvoyer vrai
      
      // Moins de 2 lettres ?
      si taille(s)<2 alors renvoyer vrai
      
      // Cas des chiffres interdits
      si bChiffresInterdits alors
      	pour i=1 _à_ taille(s)
      		si position("0123456789", s[[i]])>0 alors renvoyer vrai
      	FIN
      fin
      
      // Que des chiffres et caractères spéciaux ?
      sAlphabet est une chaine = "ABCDEFGHIJKLMNOPQRSTUVWXYZ"
      bLettre est un booleen
      pour i=1 _à_ taille(s)
      	si position(sAlphabet, Majuscule(s[[i]]))>0 alors
      		bLettre=vrai
      		sortir
      	fin
      FIN
      si pas bLettre alors renvoyer vrai
      
      // n fois la même lettre ?
      pour i=1 _à_ taille(s)
      	// Une lettre différente de la 1ère lettre => OK
      	si s[[i]]<>s[[1]] alors renvoyer faux
      FIN
      Renvoyer vrai // Pas trouvé de lettre différente de la 1ère => pas OK
      
     type : 458752
   -
     name : bTelInvalide
     procedure_id : 1541457470413812845
     type_code : 15
     code : |1+
      fonction bTelInvalide(sTelephone,sPays)
      
      // Renvoie Vrai si le numéro de tél est invalide
      
      sTel est une chaine = SansEspace(sTelephone)
      si sTel<>"" alors
          // On supprime toute ponctuation pour effectuer les contrôles de saisie
      	sTel=remplace(sTel," ","")
      	sTel=remplace(sTel,".","")
      	sTel=remplace(sTel,"-","")
      	sTel=remplace(sTel,"(","")
      	sTel=remplace(sTel,")","")
      	// numéro français ?
      	si sPays~="France" alors
      		// 01, 02, 03, 04, 05,...
      		si val(sTel[[1 à 2]])=0 ou val(sTel[[1 à 2]])>6 alors
      			si pas OuiNon(	"Les numéros de téléphone français commencent normalement par 01, 02, 03, 04, 05 ou 06"+rc+rc+...
      							"Voulez-vous enregistrer quand même le numéro saisi ?") alors  
      				renvoyer vrai
      			fin
      		FIN
      		// 10 chiffres
      		si taille(sTel)<>10 alors
      			si pas OuiNon(	"Les numéros de téléphone français comportent normalement 10 chiffres."+rc+rc+...
      							"Voulez-vous enregistrer quand même le numéro saisi ?") alors   
      				renvoyer vrai
      			fin
      		FIN
      	SINON
      		// 00 pour l'étranger
      		si pas sTel[[1 à 2]]="00" alors
      			erreur("Veuillez préfixer votre numéro de téléphone par 00 pour l'étranger.")
      			renvoyer vrai
      		FIN
      		// Au moins 5 chiffres
          	si taille(sTel)<5 alors
      			si pas OuiNon(	"Le numéro de téléphone indiqué semble incomplet."+rc+rc+...
      							"Voulez-vous enregistrer quand même le numéro saisi ?") alors   
      				renvoyer vrai
      			fin
      		FIN
      	fin
      fin
      
      renvoyer faux
      
     type : 458752
   -
     name : bVerifEmail
     procedure_id : 1943673229172649651
     type_code : 15
     code : |1+
      Fonction bVerifEmail(psEMail)
      
      SELON EmailVérifieAdresse(psEMail,emailValideSyntaxeStrict)
      	CAS emailAdresseErreur,emailAdresseRefusée,emailAdresseSyntaxeIncorrecte
      		RENVOYER Faux
      	CAS emailAdresseValide
      		RENVOYER Vrai
      	AUTRE CAS
      		RENVOYER Faux
      FIN
     type : 458752
  procedure_templates : []
  property_templates : []
 code_parameters :
  internal_properties : CAAAAAgAAACVGQgYbalU7DKC3oH4ItDrjtcTHXXV/EuH8q0IbhyQlw==
resources :
 string_res :
  identifier : 0x1fad03bb00076277
  internal_properties : CAAAAAgAAACm76HWfKGWp33VjXInA4cRlqArlgTTA862QGt72W2ld5Y=
custom_note :
 internal_properties : CAAAAAgAAABtB9HWVzrXO2+4NDRVK0vmzaNKrCKqH1DBX30lMmGZ
associated_test :
 name : TEST_Procedures_globales.wxt
